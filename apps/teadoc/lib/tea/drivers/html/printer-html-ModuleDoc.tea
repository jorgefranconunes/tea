###########################################################################
#
# Copyright (c) 2000-2013 PDM&FC, All Rights Reserved.
#
###########################################################################

import "drivers/html/html-utils.tea"





define NAVIGATION_BAR "
<table class=\"docHeader\" border=\"0\" cellpadding=\"0\" cellspacing=\"0\" width=\"100%%\">
  <tr>
    <td class=\"docHeaderCell\"><a href=\"index.html\">Overview</a></td>
    <td class=\"docHeaderCellSelected\">Module</td>
    <td class=\"docHeaderCell\">Function</td>
    <td class=\"docHeaderCell\">Class</td>
    <td width=\"100%%\" class=\"docHeaderTitle\">%s</td>
  </tr>
</table>
"

define DESCRIPTION_PREFIX "
<h1 class=\"docTitle\">
Module <i>%s</i>
</h1>
"

define DESCRIPTION_SUFFIX "
<p></p>
"

define TABLE_HEADER "
<table border=\"0\" cellpadding=\"0\" cellspacing=\"0\" width=\"100%%\">
<tr><td bgcolor=\"gray\">

<table border=\"0\" cellpadding=\"3\" cellspacing=\"1\" width=\"100%%\">
<tr>
<td colspan=\"2\" class=\"docTableHeader\">%s</td>
</tr>
"

define TABLE_FOOTER "
</table>

</td></tr>
</table>

<br>
"

define LINE_HEADER "
<TR>
<TD WIDTH=\"15%%\" CLASS=\"docTableLabel\" nowrap><A HREF=\"%s-%s.html\" CLASS=\"docTableLabel\">%s</A></TD>
<TD CLASS=\"docTableRow\">
"

define LINE_FOOTER "
</TD>
</TR>
"





###########################################################################
#
# 
#
###########################################################################

global printer-html-ModuleDoc ( moduleDoc outputDir ) {

    define fileBasename [str-cat "module-" [$moduleDoc getName] ".html"]
    define fileName     [file-join $outputDir $fileBasename]
    define outputStream [new TFileOutput $fileName]

    print-header      $outputStream $moduleDoc
    print-description $outputStream $moduleDoc
    print-functions   $outputStream $moduleDoc
    print-classes     $outputStream $moduleDoc
    print-footer      $outputStream $moduleDoc

    $outputStream close

    foreach functionDoc [$moduleDoc getFunctionDocs] {
        define funcName        [$functionDoc getName]
        define funcNameEncoded [url-encode $funcName]
        define fileBasename    [str-cat "function-" $funcNameEncoded ".html"]
        define fileName        [file-join $outputDir $fileBasename]

        $stdout writeln "Generating documentation for function " $funcName
        define output   [new TFileOutput $fileName]
        $functionDoc print $output
        $output close
    }

    foreach classDoc [$moduleDoc getClassDocs] {
        define className        [$classDoc getName]
        define classNameEncoded [url-encode $className]
        define fileName [file-join $outputDir [str-cat "class-" $classNameEncoded ".html"]]

        $stdout writeln "Generating documentation for class " $className
        define output   [new TFileOutput $fileName]
        $classDoc print $output
        $output close
    }
}





###########################################################################
#
# 
#
###########################################################################

define print-header ( outputStream moduleDoc ) {

    define now    [new TDate]
    define header [printer-html-get header]

    $outputStream write [str-fmt $HTML_DOC_PREFIX $now [$moduleDoc getName]]
    $outputStream printf $NAVIGATION_BAR $header
}





###########################################################################
#
# 
#
###########################################################################

define print-description ( outputStream moduleDoc ) {

    $outputStream printf $DESCRIPTION_PREFIX        \
                                [html-encode [$moduleDoc getName]]
    [$moduleDoc getDescription] print $outputStream
    define since       [$moduleDoc getSince]
    if {not-null? $since} {
        $outputStream writeln "<p></p>"
        $outputStream writeln "\n<DL>"
        $outputStream writeln "<DT><SPAN CLASS=\"docParamLabel\">Since:</SPAN>"
        $since print $outputStream
        $outputStream writeln "</DL>"
    }
    $outputStream printf $DESCRIPTION_SUFFIX
}





###########################################################################
#
# 
#
###########################################################################

define print-functions ( outputStream moduleDoc ) {

    define functionDocs [$moduleDoc getFunctionDocs]

    if { not-empty? $functionDocs } {
        $outputStream printf $TABLE_HEADER "Function Summary"
        foreach functionDoc $functionDocs {
            define funcName        [$functionDoc getName]
            define funcNameEncoded [url-encode $funcName]
            $outputStream printf $LINE_HEADER     \
                    "function"                    \
                    [url-encode $funcNameEncoded] \
                    [html-encode $funcName]
            [$functionDoc getOverview] print $outputStream
            $outputStream printf $LINE_FOOTER
        }
        $outputStream printf $TABLE_FOOTER
    }
}





###########################################################################
#
# 
#
###########################################################################

define print-classes ( outputStream moduleDoc ) {

    define classDocs [$moduleDoc getClassDocs]

    if { not-empty? $classDocs } {
        $outputStream printf $TABLE_HEADER "Class Summary"
        foreach classDoc $classDocs {
            define className [$classDoc getName]
            define classNameEncoded [url-encode $className]
            $outputStream printf $LINE_HEADER                                  \
                                        "class"                                  \
                                        [url-encode $classNameEncoded]    \
                                        [html-encode $className]
            [$classDoc getOverview] print $outputStream
            $outputStream printf $LINE_FOOTER
        }
        $outputStream printf $TABLE_FOOTER
    }
}





###########################################################################
#
# 
#
###########################################################################

define print-footer ( outputStream moduleDoc ) {
    
    define footer [printer-html-get footer]
    define bottom [printer-html-get bottom]

    $outputStream printf $NAVIGATION_BAR $footer
    $outputStream writeln $bottom
    $outputStream write $HTML_DOC_SUFIX
}





###########################################################################
#
# 
#
###########################################################################

